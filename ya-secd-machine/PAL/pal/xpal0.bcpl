// XPAL0 LAST MODIFIED ON FRIDAY, 12 JUNE 1970
// AT 5:37:26.84 BY R MABEE
>>> FILENAME 'XPAL0'
//
//	***********
//	*         *
//	*  XPAL0  *
//	*         *
//	***********
//
>>> GET 'XPALHD'
>>> EJECT
// XPAL0
LET XPAL() BE
$(1	CONTROL(OUTPUT, 3)
	WRITES('XPAL 5 ENTERED*N*N')
	STRP, STRB := STORAGET, CODEFILEP
	CODEP := STORAGE
	NAMECHAIN := 0
$(	LET ERRORSTR, LKPSTR = 'SYSTEMERROR', 'LOOKUPNO'
	NAMERES := '**RES**'
	MAPLIBLIST(LIBNAME)
	LIBNAME(NAMERES, O)
	LIBNAME(ERRORSTR, 0)
	LIBNAME(LKPSTR, 0)
	RV CODEFILEP := ENDOFSTREAMCH
	$(	LET V = VEC 1000
		PARV := V
		$(	LET V = VEC 2002
			REFV, REFT := V, 2000
			LOAD () $) $)
	CODEP*(0) := R_FINISH
	LISTV, LISTP := CODEP+1, CODEP+1
	LISTT, LISTL := STRP-1, STRP-1
$(	LET V = VEC 150
	LINEV, LINEP, LINET := V, V+80, V+79
	E, C, A, B := 0, STORAGE, 0, 0
	GCMARK := 0
	MAPLIBLIST(DECLLIB)
	GUESSRV := LIST(2,GUESS)
	TRUERV := LIST(2, M_TRUE)
	FALSERV := LIST(2, M_FALSE)
	NILRV := LIST(2, M_TUPLE, 0)
	DUMMYRV := LIST(2, M_DUMMY)
	NILSRV := LIST(2, NILS)
	E := LIST(5, ENV, E, 0, E) // DUMMY ENV NODE USED BY DIAGNOSE
	A := LIST(3, BASICFN, DIAGNOSE)
	ERRORLV := LIST(3, LVALUE, A)
	E := LIST(5, ENV, E, ERRORSTR, ERRORLV)
	LOOKUPNO := LIST(3, NUMBER, 0)
	A := LIST(3, LVALUE, LOOKUPNO)
	E := LIST(5, ENV, E, LKPSTR, A)
	ERRFLAG, FLOTERR := FALSE, FALSE
	LISTV := LISTP
	RESTARTC := 0
	NSET := FALSE
	COUNT := 0
	ERRCT := 0
	$(	(C*(0))()
		COUNT := COUNT+1
		IF TIME_EXCEEDED DO TIMEOVFL() $)
	REPEATWHILE COUNT<MAXCT
	WRITES('*N*NEXECUTION CYCLE LIMIT EXCEEDED*N')
	TERMINATE() $)1
